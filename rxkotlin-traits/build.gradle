apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.JurajBegovac'

// Manifest version information!
class Version {
    static major = 2
    static minor = 0
    static patch = 0
    static build = 1 // bump for dogfood builds, public betas, etc. - increase this every time

    // Pre-release identifiers that are appended to version name.
    // For example: ${versionName}-alpha, ${versionName}-alpha.1, ${versionName}-rc.2, etc.
    static preReleaseIdentifier = '' // [alpha|beta|rc]
    static preReleaseStage = 0

    static asCode() {
        return build
    }

    static asName() {
        def name = "${major}.${minor}.${patch}"
        if (preReleaseIdentifier?.trim()) {
            name = "${name}-${preReleaseIdentifier}"
            if (preReleaseStage > 0) name = "${name}.${preReleaseStage}"
        }
        return "${name} (${build})"
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 25
        versionCode Version.asCode()
        versionName Version.asName()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Rx
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'
    compile('io.reactivex.rxjava2:rxandroid:2.0.1') {
        exclude module: 'rxjava2'
    }
    compile('io.reactivex.rxjava2:rxkotlin:2.1.0') {
        exclude module: 'rxjava2'
        exclude module: 'kotlin-stdlib'
        exclude module: 'kotlin-runtime'
    }

    testCompile 'junit:junit:4.12'
    testCompile(project(':testutils')) {
        exclude group: 'com.android.support'
        exclude group: 'io.reactivex.rxjava2'
        exclude group: 'junit'
        exclude module: 'kotlin-stdlib'
        exclude module: 'kotlin-runtime'
    }

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
repositories {
    mavenCentral()
}
